;;;; Functions used in parsing the programming language
;;;; used for program checking.

(defpackage #:parser
  (:use #:common-lisp #:cl-ppcre #:sb-gray))
(in-package #:parser)

;;; ******************************************
;;; Constants
;;; ******************************************

(defconstant var-regex "[a-zA-Z](?:\\d|\\w)*")
(defconstant reserved-keywords "true|false|if|else|while")
(defconstant usage-message
  "usage: program-checker <precondition> <program> <postcondition>")
(defconstant error-message
  "The program is not valid. Please verify the syntax and try again.")

;;; ******************************************
;;; Data structures
;;; ******************************************

(defstruct command var expression)

;;; ******************************************
;;; Helper functions
;;; ******************************************

;;; Helper function for checking if two chars
;;; are equal with the possibility of char1 or
;;; char2 being nil
(defun char-eq (char1 char2)
  (and  
   (not
    (or (null char1) (null char2)))
   (char= char1 char2)))

(defun extract-strings (input starts ends)
  (loop for i from 0 to (1- (length starts))
   for start = (aref starts i)
   for end = (aref ends i)
     collect (subseq input start end)))

(defun match (regex input)
  (multiple-value-bind (start end starts ends)
      (scan regex input)
    (if (or (null start) (null end))
	nil
	(values
	 (and
	  (= 0 start)
	  (= (length input) end))
	 (extract-strings input starts ends)))))

(defun read-while (stream regex)
  (format nil "狺祜镳骘汨狎疱咫汨狎铋篝蝈犴铋飑麒殪磲翥蝈珏ㄦ矧磲铋幄汨狎┅麒殪铒铛祆汨狎┅滹蝈徜汨狎篝蝈犴铋飑泔祆邈汨狎┅换嗅蝮弩犷轭翦珏骝镯篝蝈犴ㄤ彐躅疳蝮瀛轭篝蝈犴戾è汨狎蝈徜麒殪篝蝈犴③苘洵茛┅ㄩ磲翥寇茕汨狎螬鲠祯弩汨狎螬┅换嗅蝮弩鲠蜷徕戾殇孱糸骈弪骝镯篝蝈犴ㄤ彐躅疳蝮瀛鲠篝蝈犴戾è汨狎蝈徜麒殪篝蝈犴③苘鬈茕茛┅ㄩㄡ钿磲翥鲠颦蝈珏汨狎螬铒磲翥蝈箦蝣邃脲黠蜾汨狎螬┅鲠祯弩汨狎螬┅换换砒痱弩箝镱疳蝮轭骢钽糸镱换换嗅蝮弩翳镳弪狒矧犷翳箦泔钿镳弪犷换镦磲翳屙狒殂犰屮痱弩箝镱骝镯篝蝈犴ㄤ彐躅疳蝮瀛磲翳屮痱弩箝镱篝蝈犴戾è汨狎蝈徜汨狎篝蝈犴铋飑蝈篚祠┅ㄩㄡ钿矧ㄣ栳颦羼汨狎＼ㄣ栳颦羼汨狎＼ㄣ栳颦羼汨狎＼┅眭祠轲戾鲠祯瀛忾钿疳蝮瀛蝈篚祠鲠祯濠疳蝮瀛屮痱弩箝镱篝蝈犴箦翩蝈篚祠鲠祯濠疳蝮瀛蝈篚祠┅鲠祯弩ㄦ矧磲铋狺幄汨狎蝈篚祠┅┅ㄤ彐躅疳蝮瀛泔眇戾屮痱弩箝镱篝蝈犴换俞鲥翳沲蝌孱痫箝糸镱镦翳篝蝈犴戾è篝蝈犴痫篝蝈犴骈戾痫箝糸镱篝蝈犴┅ㄥ痱蝈篚祠磲翳蝈篚祠┅ㄩㄡ钿眭祠轲戾鲠祯瀛忾钿疳蝮瀛蝈篚祠鲠祯濠疳蝮瀛屮痱弩箝镱篝蝈犴箦翩屮痱蝈篚祠鲠祯濠疳蝮瀛蝈篚祠眭祠轲戾鲠祯瀛忾钿疳蝮瀛蝈篚祠鲠祯濠疳蝮瀛磲翳屮痱弩箝镱篝蝈犴箦翩磲翳蝈篚祠鲠祯濠疳蝮瀛蝈篚祠┅鲠祯弩ㄦ矧磲铋狺幄屮痱蝈篚祠磲翳蝈篚祠┅痱镧换义篝矧翳篝蝈犴麸轸矧殓轭犰痫箝糸镱篝蝈犴骈戾痫箝糸镱篝蝈犴篝蝈犴痫螬ㄩㄡ钿ㄣ栳颦羼蝈徜汨狎篝蝈犴铋飑＼眭祠轲戾鲠祯瀛忾钿疳蝮瀛蝈篚祠鲠祯濠疳蝮瀛屮痱弩箝镱篝蝈犴箦翩屮痱蝈篚祠鲠祯濠疳蝮瀛蝈篚祠┅鲠祯弩ㄦ矧磲铋幄屮痱蝈篚祠┅┅┅ㄤ彐躅疳蝮瀛疳蝈铘桢箝邃屮痱弩箝镱篝蝈犴戾è蝈篚祠┅ㄩㄡ钿ㄣ栳颦羼蝈徜汨狎篝蝈犴铋飑＼ī眭祠轲戾鲠祯瀛忾钿疳蝮瀛蝈篚祠鲠祯濠疳蝮瀛泔眇戾屮痱弩箝镱篝蝈犴箦翩蝈篚祠鲠祯濠疳蝮瀛蝈篚祠ㄣ栳颦羼蝈徜汨狎篝蝈犴铋飑＼┅鲠祯弩ㄦ矧磲铋岍蝈篚祠┅┅ㄤ彐躅疳蝮瀛屮痱弩箝镱篝蝈犴戾è汨狎ㄦ矧磲铋幄疱咫汨狎铋篝蝈犴铋飑┅眭祠轲戾鲠祯瀛忾钿疳蝮瀛蝈篚祠鲠祯濠ㄣ镱è磲翥③苘洵茛汨狎疳蝮瀛轭篝蝈犴┅è磲翥④荀汨狎疳蝮瀛鲠篝蝈犴┅è磲翥④塄汨狎疳蝮瀛疳蝈铘桢箝邃屮痱弩箝镱篝蝈犴┅鲠祯弩疳蝮瀛蝈篚祠鲠祯濠┅换换嘛镬遽疳蝮轭骢钽糸镱换ㄤ彐躅疳蝮瀛泔眇秕钿怙镬遽篝蝈犴戾è汨狎蝈徜汨狎篝蝈犴铋飑┅ㄡ钿矧ㄣ栳颦羼汨狎＼Ιㄡ钿ㄣ栳颦羼汨狎＼ㄣ栳颦羼蝈徜汨狎篝蝈犴铋飑＼┅疳蝮瀛怙镬遽篝蝈犴┅┅ㄤ彐躅疳蝮瀛泔眇戾怙镬遽篝蝈犴戾è汨狎疱咫汨狎铋篝蝈犴铋飑┅ㄩㄣ栳颦羼汨狎＼々ㄡ钿ㄣ栳颦羼蝈徜汨狎篝蝈犴铋飑＼々疳蝮瀛怙镬遽篝蝈犴┅换俞鲥翳痫箝糸镱镦翳篝蝈犴骘祜镫徼遽戾è篝蝈犴痫篝蝈犴骈戾痫箝糸镱篝蝈犴┅ㄩ疳蝮瀛屮痱弩箝镱篝蝈犴ㄡ钿ㄣ栳颦羼蝈徜汨狎篝蝈犴铋飑＼缉疳蝮瀛屮痱弩箝镱篝蝈犴┅痱镧换义篝矧翳篝蝈犴狒轸矧殓轭犰痫箝糸镱篝蝈犴骈戾痫箝糸镱篝蝈犴篝蝈犴痫螬ㄡ钿疳蝮瀛怙镬遽篝蝈犴疳蝮瀛泔眇秕钿怙镬遽篝蝈犴┅┅┅┅ㄤ彐躅疳蝮瀛怙镬遽篝蝈犴ㄩㄣ栳颦羼疱咫汨狎铋篝蝈犴铋飑＼īㄡ钿ㄣ栳颦羼蝈徜汨狎篝蝈犴铋飑＼ī疳蝮瀛泔眇戾怙镬遽篝蝈犴ㄣ栳颦羼蝈徜汨狎篝蝈犴铋飑＼┅戾è鲠祯蝈徜麒殪篝蝈犴③趄蹂驷祗茛┅矧磲翥Ⅳ蝓澧鲠祯濠磲翥㈡犰箦鲠祯濠┅┅换换蔑眄犷疳蝮轭骢钽糸镱换ㄤ彐躅疳蝮瀛箝眇戾泔眄犷篝蝈犴戾è泔眄磲脲泔眄犷洎┅ㄩㄡ钿眭祠轲戾鲠祯瀛忾钿鲠颦疳蝮邃鲠颟疳蝮瀛鲠篝蝈犴箦翩ㄣ镯磲钿鲠泔眄鲠颟鲠颦疳蝮邃ㄣ栳颦羼蝈徜汨狎篝蝈犴铋飑＼僵眭祠轲戾鲠祯瀛忾钿ㄥ痱疳蝮邃屮痱疳蝮瀛屮痱弩箝镱篝蝈犴箦翩ㄣ镯磲钿屮痱弩箝镱泔眄屮痱屮痱疳蝮邃┅泔眄┅ㄤ彐躅疳蝮瀛泔眄犷篝蝈犴祜镳骘泔眄疳蝮瀛箝眇戾泔眄犷篝蝈犴骘汨狎痱镧蝈徜麒殪篝蝈犴④荏蝈徜汨狎篝蝈犴铋飑骘铄舡汨狎痱镧蝈徜麒殪篝蝈犴④荏疱咫汨狎铋篝蝈犴铋飑骘轶箦黹泔祜ㄣ栳颦羼汨狎＼哗骘铛祆汨狎铛祆汨狎泔祆邈泔眄殒矧铛祆泔眄铒矧轶箦黹泔祜铛祆汨狎┅蝈趱蝾铋麒殪铒矧铛祆汨狎ㄡ钿轶箦黹泔祜铛祆铄舡汨狎┅┅┅换换蔑眄犷怩忖扉铉骢钽糸镱换ㄤ彐躅怩忖戾狍箝珙礤铘ㄣ镯磲钿痱镳泔钿蝈珏蝈痨徙瀛犰ㄣ镯磲钿鲠泔眄犷洎痱镳泔钿ㄣ镯磲钿屮痱弩箝镱泔眄犷洎┅ㄤ彐躅怩忖戾泔眄犷潴ㄣ镯磲钿痫篝泔钿轸轱瞟ㄩ铛祆泔眄犷潴铋戾è蝈鲥蝮瀛泔眄犷潴蝈鲥蝮泔眄犷潴┅ㄣ躜蝈铘痱镳泔钿轸轱痫篝泔钿轸轱瞟祜镳骘泔眄犷轭蝈鲥蝮瀛泔眄犷潴殒ㄣ镯磲钿泔眄犷洎滹箦翩沲蝌孱舡痱镳泔钿轸轱ㄢ踱忪瀛狍箝珙礤铘泔眄犷沲蝌孱舡痱镳泔钿轸轱瞟┅沲蝌孱舡痱镳泔钿轸轱瞟┅换换歪轭骢钽糸镱换ㄤ彐躅怩殪洵疳蝮瀛趄邋ㄩ铕豸戾è篝蝈犴磲脲篝蜷铉轭瘐舡篝蝈犴轭瘐舂┅疳蝮瀛泔眄犷篝蝈犴┅ㄤ彐躅汨邈氕痱镧蜥痱邈镱溟糸镱痱镧蜥痫篝泔钿轸轱瞟戾è蝈篚祠ㄢ踱忪瀛泔眄犷潴ㄢ蹰熹疳蝮瀛趄邋痱镧蜥愆痫篝泔钿轸轱瞟┅ㄩ铛祆蝈篚祠铋ㄦ矧磲铋幄痱邈镱溟糸镱蝈篚祠┅┅ㄤ彐躅磲轭ㄡ蜱雯ㄩ铒戾铉翳狎琏穿痱镧黩轸瀛扉铄躞徵瀛礤篌徵濠筲屮艉屮轸┅戾舄è痱邈镱溟糸镱铘狎琏┅痱镧蜥铘狎琏┅痫篝泔钿轸轱铘狎琏┅蝈篚祠ㄣ桢汶痱镧蜥痱邈镱溟糸镱痱镧蜥痫篝泔钿轸轱瞟┅ㄩ铛祆蝈篚祠黩轸瀛扉铄弪蝻颦礤篌徵濠黩轸瀛扉铄蝈篚祠┅筲屮艉屮轸┅┅