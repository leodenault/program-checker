;;;; Functions used in parsing the programming language
;;;; used for program checking.

(defpackage #:parser
  (:use #:common-lisp #:cl-ppcre #:sb-gray))
(in-package #:parser)

;;; ******************************************
;;; Constants
;;; ******************************************

(defconstant var-regex "[a-zA-Z](?:\\d|\\w)*"
  "Regex that represents a valid variable name")
(defconstant reserved-keywords "true|false|if|else|while|T|F"
  "Keywords reserved to the parsed language that a user may not use to define varibales")
(defconstant usage-message
  "usage: program-checker <precondition> <program> <postcondition> [<invariants>] [<variants>]"
  "Message displayed when running the program from the command line if an incorrect number of arguments is passed")
(defconstant error-message
  "The program is not valid. Please verify the syntax and try again."
  "Error displayed if there is a syntax error with the given program")
(defconstant inv-var-error "The numbers of invariants and variants don't match. Please ensure to input equal numbers of each."
  "Error displayed when the numbers of invariants and variants don't match")
(defconstant math-operators '(#\+ #\- #\*)
  "Operators used in mathematical operations for expressions")

;;; ******************************************
;;; Global Variables
;;; ******************************************

(defparameter *invariants* '()
  "List of invariants for the input program. Used for determining partial correctness proofs")
(defparameter *variants* '()
  "List of variants for the input program. Used for determining total corectness proofs ")
(defparameter *partial-proofs* '()
  "Proofs for partial correctness generated by this program")
(defparameter *total-proofs* '()
  "Proofs for total correctness generated by this program")

;;; ******************************************
;;; Data structures
;;; ******************************************

;;; x = y;
(defstruct command var expression)
;;; if boolean { ... } else { ... }
(defstruct if-command boolean then-command-list else-command-list)
;;; while boolean { ... }
(defstruct while-command boolean command-list)

;;; ******************************************
;;; Helper functions
;;; ******************************************

(defun char-eq (char1 char2)
  "Helper function for checking if two chars are equal with the
possibility of char1 or char2 being nil. Returns true if they are equal,
false otherwise"
  (and  
   (not
    (or (null char1) (null char2)))
   (char= char1 char2)))

(defun match (regex input)
  "Matches input to regex. Returns the input string if they match,
nil if they don't"
  (multiple-value-bind (start end)
      (scan regex input)
    (if (or (null start) (null end))
	nil ; The input didn't match the regex
	(and
	 (= 0 start)
	 (= (length input) end)))))

(defun read-while (stream regex)
  "Reads the stream one character at a time as long as the next character
being read matches regex. Returns the characters that were read"
  (format nil "狺祜镳骘汨狎疱咫汨狎铋篝蝈犴铋飑麒殪磲翥蝈珏ㄦ矧磲铋幄汨狎┅麒殪铒铛祆汨狎┅滹蝈徜汨狎篝蝈犴铋飑泔祆邈汨狎┅ㄤ彐躅疳蝮瀛轭篝蝈犴⑿狎箦犷轭翦珏骝镯翳篝蝈犴义趱蝾翳轭翦珏狍篝蜷铉殒轸篚沣邋潴铋雉桢蝼轶澧戾è汨狎蝈徜麒殪篝蝈犴③苘洵茛┅ㄩ磲翥寇茕汨狎螬汨狎螬┅ㄤ彐躅疳蝮瀛鲠篝蝈犴⑿狎箦鲠蜷徕戾殇孱糸骈弪骝镯篝蝈犴义趱蝾翳殇孱糸骈弪狍篝蜷铉殒轸篚沣邋潴铋雉桢蝼轶澧戾è汨狎蝈徜麒殪篝蝈犴③苘鬈茕茛┅ㄩㄡ钿磲翥鲠颦蝈珏汨狎螬铒磲翥蝈箦蝣邃脲黠蜾汨狎螬┅汨狎螬┅ㄤ彐躅疳蝮瀛戾骠怛徙篝蝈犴⑿狎箦戾骠沲蜢怛徙骝镯翳篝蝈犴义趱蝾殒轸篚沣弩箧蹯禊疳蝮邃轸铋雉桢蝼轶澧磲翥④荏苘螵蝈徜麒殪篝蝈犴③苘筝┅ㄤ彐躅疳蝮瀛蜷玷舡怛徙篝蝈犴⑿狎箦蜷玷沲蜢怛徙骝镯翳篝蝈犴义趱蝾殒轸篚沣弩箧蹯禊疳蝮邃轸铋雉桢蝼轶澧换澡轶轶轫痨屙孱翦溟骀弪孱綮骝镯疳蝮瀛戾骠怛徙换忮汜躞轸轶痫篌殁戾麸栳鲥赭轭蝻鳜狍镳痫箦换麸ㄡ钿蝈徜麒殪篝蝈犴④荏ㄣ栳颦羼蝈徜汨狎篝蝈犴铋飑＼蝈徜麒殪篝蝈犴④荏┅换换砒痱弩箝镱疳蝮轭骢钽糸镱换ㄤ彐躅疳蝮瀛磲翳屮痱弩箝镱篝蝈犴⑿狎箦翳镳弪狒矧犷翳箦泔钿镳弪犷镦磲翳屙狒殂犰屮痱弩箝镱骝镯篝蝈犴义趱蝾翳屙狍骘蝽狒翦篝蜷铉铋雉桢蝼轶澧戾è汨狎蝈徜汨狎篝蝈犴铋飑义徜翳骈蝮汨狎蝈篚祠┅ㄩㄡ钿礤礅弪汨狎磲翳镳弪狒矧螬描邈翳狒轸犷镳弪狒矧蝈徜麒殪篝蝈犴④荏箦翩蝈篚祠疳蝮瀛屮痱弩箝镱篝蝈犴┅嗅蝮犷屮痱弩箝镱ㄦ矧磲铋幄汨狎蝈篚祠┅┅ㄤ彐躅疳蝮瀛泔眇戾屮痱弩箝镱篝蝈犴⑿狎箦眭祠榄疳螋屮痱弩箝镱义趱蝾轸狍骘蝽狒翦篝蜷铉铋雉桢蝼轶澧换俞鲥翳沲蝌孱痫箝糸镱镦翳篝蝈犴戾è篝蝈犴痫篝蝈犴骈戾痫箝糸镱篝蝈犴┅ㄥ痱蝈篚祠磲翳蝈篚祠┅换则麸蝈徜翳屮痱弩箝镱狍磲翳屙狒殂犰镳弪狒轱ㄩㄡ钿箦翩屮痱蝈篚祠疳蝮瀛屮痱弩箝镱篝蝈犴┅蝈徜麒殪篝蝈犴④荏箦翩磲翳蝈篚祠疳蝮瀛磲翳屮痱弩箝镱篝蝈犴┅ㄦ矧磲铋幄屮痱蝈篚祠磲翳蝈篚祠换澎箦趄麸蝈徜翳屮痱弩箝镱狍铄玑糸镱痱镧换义篝矧翳篝蝈犴麸轸矧殓轭犰痫箝糸镱篝蝈犴骈戾痫箝糸镱篝蝈犴篝蝈犴痫螬ㄩㄡ钿ㄣ栳颦羼蝈徜汨狎篝蝈犴铋飑＼箦翩屮痱蝈篚祠疳蝮瀛屮痱弩箝镱篝蝈犴┅ㄦ矧磲铋幄屮痱蝈篚祠┅┅┅ㄤ彐躅疳蝮瀛疳蝈铘桢箝邃屮痱弩箝镱篝蝈犴⑿狎箦犷屮痱弩箝镱泔铘衢铄忮赭邋疳蝈铘桢箦螽义趱蝾轸狍骘蝽狒翦篝蜷铉铋雉桢蝼轶澧戾è蝈篚祠┅ㄩㄡ钿ㄣ栳颦羼蝈徜汨狎篝蝈犴铋飑＼ī蝈徜麒殪篝蝈犴④荏箦翩蝈篚祠疳蝮瀛泔眇戾屮痱弩箝镱篝蝈犴┅蝈徜麒殪篝蝈犴④荏ㄣ栳颦羼蝈徜汨狎篝蝈犴铋飑＼┅ㄦ矧磲铋岍蝈篚祠┅┅ㄤ彐躅疳蝮瀛屮痱弩箝镱篝蝈犴⑿狎箦麸瓠戾鲥屮痱弩箝镱骑祆秣翳挛铒翎糸镱骘躅轭汨狃翦镦翳泔躜箦怙镫汉ō农ㄅ农ㄅ农ㄅ农义趱蝾轸狍骘蝽狒翦篝蜷铉铋雉桢蝼轶澧戾è汨狎ㄦ矧磲铋幄疱咫汨狎铋篝蝈犴铋飑┅ㄣ镱è磲翥③苘洵茛汨狎疳蝮瀛轭篝蝈犴┅è磲翥④荀汨狎疳蝮瀛鲠篝蝈犴┅è磲翥④塄汨狎疳蝮瀛疳蝈铘桢箝邃屮痱弩箝镱篝蝈犴┅┅换换嘛镬遽疳蝮轭骢钽糸镱换ㄤ彐躅疳蝮瀛泔眇秕钿怙镬遽篝蝈犴⑿狎箦翳怙镬遽镳弪狒矧犷翳箦泔钿怙镬遽镳弪犷洚义趱蝾翳屙狍骘蝽狒翦篝蜷铉铋雉桢蝼轶澧戾è汨狎蝈徜汨狎篝蝈犴铋飑┅换涉轸蝈徜Μ溟蝈泗禊狒翦眇麸蝈徜翳铄麸脲换狍怙镬遽ㄣ镱è汨狎羼汨狎＼Ι蝈徜麒殪篝蝈犴④荏ㄦ矧磲铋幄＼疳蝮瀛怙镬遽篝蝈犴┅换涉轸蝈徜疳蝮犷雉桢翳孱疳蝮犷屮痱弩箝镱è犷ㄣ栳颦羼汨狎＼ㄣ栳颦羼蝈徜汨狎篝蝈犴铋飑＼┅蝈徜麒殪篝蝈犴④荏ㄦ矧磲铋岑幄＼疳蝮瀛怙镬遽篝蝈犴┅┅┅ㄤ彐躅疳蝮瀛泔眇戾怙镬遽篝蝈犴⑿狎箦眭祠榄疳螋怙镬遽町澡轶翎脲汜蝈镦犷泔眇狎轶镱忮赭邋赭怙镬遽铙矧屮痱弩箝镱螽义趱蝾轸狍骘蝽狒翦篝蜷铉铋雉桢蝼轶澧戾è汨狎疱咫汨狎铋篝蝈犴铋飑┅换崎蝮狒翦眇麸蝈徜翳怙镬遽狍铄玑糸镱ㄩㄣ栳颦羼汨狎＼々痱镧蝈徜汨狎篝蝈犴铋飑ㄦ矧磲铋狺幄＼疳蝮瀛怙镬遽篝蝈犴┅换涉铒衄筢鲥翳痫箝糸镱镦翳篝蝈犴骘祜镫徼遽戾è篝蝈犴痫篝蝈犴骈戾痫箝糸镱篝蝈犴┅镳汨狎铋飑蝈篚祠┅换物趄疳蝮轭翳怙镬遽狍泔眇狎轶镱忮赭邋屮痱弩箝镱ㄩ箦翩蝈篚祠疳蝮瀛屮痱弩箝镱篝蝈犴┅痱镧蝈徜麒殪篝蝈犴④荏箦翩镳汨狎蝈徜汨狎篝蝈犴铋飑换描邈骘犷ㄩㄡ钿矧ㄣ栳颦羼镳汨狎＼缉ㄣ栳颦羼镳汨狎＼京铒ㄣ栳颦羼疱咫汨狎铋篝蝈犴铋飑＼僵┅痱镧蝈徜麒殪篝蝈犴④荏ㄦ矧磲铋幄蝈篚祠镳汨狎疳蝮瀛屮痱弩箝镱篝蝈犴┅换澎箦汨邈骘冀窘〗矧浇戾è铄舡汨狎蝈徜汨狎篝蝈犴铋飑┅蝈徜麒殪篝蝈犴④荏ㄣ镱è犷ㄣ栳颦羼镳汨狎＼缉ㄣ栳颦羼铄舡汨狎＼僵ㄦ矧磲铋冀幄蝈篚祠疳蝮瀛屮痱弩箝镱篝蝈犴┅è犷ㄣ栳颦羼镳汨狎＼京ㄣ栳颦羼铄舡汨狎＼僵ㄦ矧磲铋窘幄蝈篚祠疳蝮瀛屮痱弩箝镱篝蝈犴┅è犷ㄣ栳颦羼镳汨狎＼々ㄣ栳颦羼铄舡汨狎＼僵ㄦ矧磲铋〗幄蝈篚祠疳蝮瀛屮痱弩箝镱篝蝈犴┅è犷ㄣ栳颦羼镳汨狎＼僵ㄣ栳颦羼铄舡汨狎＼僵ㄦ矧磲铋幄蝈篚祠疳蝮瀛屮痱弩箝镱篝蝈犴┅┅┅换涉轸泔蹯漕疳蝮狍泔眇狎轶镱忮赭邋屮痱弩箝镱蟋换翳孱趄疳蝮轭狍泔眇狎轶镱忮赭邋怙镬遽铙痱镧换义篝矧翳篝蝈犴狒轸矧殓轭犰痫箝糸镱篝蝈犴骈戾痫箝糸镱篝蝈犴篝蝈犴痫螬ㄩㄡ钿箦翩蝈篚祠疳蝮瀛怙镬遽篝蝈犴┅蝈徜麒殪篝蝈犴④荏┅ㄦ矧磲铋幄蝈篚祠疳蝮瀛泔眇秕钿怙镬遽篝蝈犴┅┅┅┅ㄤ彐躅疳蝮瀛怙镬遽篝蝈犴⑿狎箦麸瓠戾鲥怙镬遽屮痱弩箝镱骑祆秣犷屮翦钿邃鲥蝮轱镦翳挛铒翎糸镱骘躅轭汨狃翦镦翳泔躜箦怙镫汉趄蹂驷祗ā漏漏漏ㄅ农ㄅ农ㄅ冀农ㄅ窘农ㄅ浇农ㄅ〗农义趱蝾轸狍骘蝽狒翦篝蜷铉铋雉桢蝼轶澧ㄩㄣ栳颦羼疱咫汨狎铋篝蝈犴铋飑＼ī戾è蝈篚祠┅换则疳蝮轭狍泔眇戾怙镬遽ㄩㄡ钿ㄣ栳颦羼蝈徜汨狎篝蝈犴铋飑＼ī蝈徜麒殪篝蝈犴④荏箦翩蝈篚祠疳蝮瀛泔眇戾怙镬遽篝蝈犴┅蝈徜麒殪篝蝈犴④荏ㄣ栳颦羼蝈徜汨狎篝蝈犴铋飑＼┅ㄦ矧磲铋岍蝈篚祠┅换萧桢蝼轶瀣趄疳蝮轭狍翳痱轫轸轹弩趄蹂矧驷祗瀣犷蝈趱蝾换翎豸镬镧ㄔ矧泔铘蜥溟泗轱ㄆ┈蝈箴邈糸鲥禊戾è鲠祯蝈徜麒殪篝蝈犴③趄蹂驷祗茛┅ㄣ镱è磲翥Ⅳ蝓澧鲠祯濠⒃è磲翥㈡犰箦鲠祯濠⑵┅┅换换蔑眄犷疳蝮轭骢钽糸镱换换蔑眄犷疳蝮轭骢钽糸镱蝈趱蝾泔眄犷溽翎篝蝓泗躜弩换泔蝌弩痫钿轭麸翳豉疱镦泔眄犷忮轭疳蝮邃ㄤ彐躅疳蝮瀛狍箝珙礤铘篝蝈犴⑿狎箦犷狍箝珙礤铘篝狒屙孱镦翳骘祆秣轭骘蝽鲠鲠祯义趱蝾泔眄犷溽翎篝蝓泗躜蝈痱弩孱糸铉翳疳蝮邃泔溴殒轸篚沣邋潴雉桢蝼轶蝈趱蝾铋膦戾è泔眄磲脲泔眄犷洎┅ㄩㄡ钿箦翩ㄣ镯磲钿鲠泔眄疳蝮瀛鲠篝蝈犴┅蝈徜麒殪篝蝈犴④荏ㄣ栳颦羼蝈徜汨狎篝蝈犴铋飑＼僵蝈徜麒殪篝蝈犴④荏箦翩ㄣ镯磲钿屮痱弩箝镱泔眄疳蝮瀛屮痱弩箝镱篝蝈犴┅泔眄┅ㄤ彐躅疳蝮瀛殒篝蝈犴⑿狎箦犷殒篝狒屙孱镦翳骘祆秣轭骘蝽殒怙镬遽屐箦义趱蝾犷殒泔眄犷溽翎篝蝓泗躜蝈痱弩孱糸铉翳疳蝮邃泔溴殒轸篚沣邋潴雉桢蝼轶蝈趱蝾铋膦戾è汨狎痱镧换义徜㈤姊骝镯翳篝蝈犴ㄦ矧磲铋狺幄蝈徜汨狎篝蝈犴铋飑蝈徜汨狎篝蝈犴铋飑┅ㄣ镯磲脲殒泔眄犷洎┅ㄩㄡ钿磲翥㈤姊汨狎螬蝈徜麒殪篝蝈犴④荏换嗅蝮翳怙镬遽箦翩ㄩ姝泔眄犷洵怙镬遽泔眄疳蝮瀛怙镬遽篝蝈犴┅疳蝮瀛戾骠怛徙篝蝈犴换嗅蝮翳Ⅳ桢睥泔眄犷扉篝箦翩ㄩ姝泔眄犷洵翳孱泔眄犷洵扉篝泔眄疳蝮瀛泔眄犷洵扉篝篝蝈犴┅疳蝮瀛蜷玷舡怛徙篝蝈犴磲翥㈠祗澧蝈徜麒殪篝蝈犴③屐箦茛┅疳蝮瀛戾骠怛徙篝蝈犴换嗅蝮翳㈠祗澧泔眄犷扉篝箦翩ㄩ姝泔眄犷洵屐箦泔眄犷洵扉篝泔眄疳蝮瀛泔眄犷洵扉篝篝蝈犴┅疳蝮瀛蜷玷舡怛徙篝蝈犴┅泔眄┅ㄤ彐躅疳蝮瀛麒殪篝蝈犴⑿狎箦麒殪篝狒屙孱镦翳骘祆秣轭骘蝽麒殪怙镬遽义趱蝾麒殪瀛泔眄犷溽翎篝蝓泗躜蝈痱弩孱糸铉翳疳蝮邃泔溴殒轸篚沣邋潴雉桢蝼轶蝈趱蝾铋膦戾è汨狎蝈徜麒殪篝蝈犴③麒殪遢┅ㄣ镯磲脲麒殪瀛泔眄犷洎┅ㄩㄡ钿磲翥Ⅶ栝戾汨狎螬蝈徜麒殪篝蝈犴④荏换嗅蝮翳怙镬遽箦翩麒殪瀛泔眄犷洵怙镬遽泔眄疳蝮瀛怙镬遽篝蝈犴┅疳蝮瀛戾骠怛徙篝蝈犴换嗅蝮翳轭铄泔眄犷扉篝箦翩麒殪瀛泔眄犷洵泔眄犷洵扉篝泔眄疳蝮瀛泔眄犷洵扉篝篝蝈犴┅疳蝮瀛蜷玷舡怛徙篝蝈犴┅泔眄┅ㄤ彐躅疳蝮瀛泔眄犷篝蝈犴⒃镳戾鲥疳蝮轭骢钽糸镱骘疳蝮轭溟骀弪孱泔眄犷潴拈骀弪孱糸狒弩忮赭邋犷狍箝珙礤铘犷殒篝狒屙孱衄犷麒殪篝狒屙孱舢义趱蝾翳溽翎篝蝓泗躜泔蝌弩痫钿轭溽翎篝蝓泗躜殒轸篚沣邋潴雉桢蝼轶蝈趱蝾铋膦换义徜翳骈蝮驽汨狎徙翦蝮犷翳孱蝈箦翳篝蝈犴换麸腩秣栾麸疳蝮翳泔眄犷潴戾è篝蝈犴痫篝蝈犴骈戾痫箝糸镱篝蝈犴┅ㄣ栳蝮蝈徜麒殪篝蝈犴③苘鬈茕茛┅篝蝈犴骈戾痫箝糸镱篝蝈犴篝蝈犴痫螬ㄣ镱è磲翥㈤姊汨狎螬疳蝮瀛殒篝蝈犴┅è磲翥Ⅶ栝戾汨狎螬疳蝮瀛麒殪篝蝈犴┅疳蝮瀛狍箝珙礤铘篝蝈犴┅┅ㄤ彐躅疳蝮瀛泔眄犷洵扉篝篝蝈犴⑿狎箦扉篝镦泔眄犷潴箦疳蜥翦怡箦黹泔祜铙澡骈钺泔眄犷汜镳糸镱犰禊忮骘祆秣邃怡箦黹泔祜町涉轸篚沣邋潴蝈趱蝾扉篝镦溽翎篝蝓泗躜弩遽汨泔蝌弩痫钿轭麸泔眄犷翳狒麽蝈徜祜镳骘泔眄疳蝮瀛泔眄犷篝蝈犴骘汨狎痱镧蝈徜麒殪篝蝈犴④荏疱咫汨狎铋篝蝈犴铋飑骘轶箦黹泔祜ㄣ栳颦羼汨狎＼哗骘轶泔眄犷ㄣ镯磲钿泔眄骘泔铘轭蹂麒殪铒铛祆泔眄┅馏祜铉狍泔眄犷潴狎鲠扉泔祆邈泔眄换涉孱镦篝蝈犴轶蝈徙桢洮篝镳疳蝮轭滹ㄩ轶泔眄犷ㄩ轶箦黹泔祜蝈徜汨狎篝蝈犴铋飑箦翩泔铘轭蹂铋飑┅滹蝈徜麒殪篝蝈犴④荏麒殪泔铘轭蹂┅换换蔑眄犷怩忖扉铉骢钽糸镱换换澡弩骢钽糸镱狎躞邃麸怩忖戾躔翳蝻蹒翳泔眄犷潴换骝镯灬篝麸骈蝮狍箦孱轭翳泔躜箦澡妁磲铋瘐灬翦换翳泔眄犷潴麸秕麴豸翳骘蝽蹯狍翳狒鏖祆忮铄邃邃麸换痱秭翳鲠扉溟豉镦翳痱镧蜥ㄤ彐躅怩忖戾狍箝珙礤铘ㄣ镯磲钿痱镳泔钿⒘痧扉弩翳狍箝珙礤铘蝓戾躞轭泔眄犷麸翳玳鲥痱镳矬轸轱钺祜玳骘蝽蹯岈痱镳泔钿换义痨徙犷镢沲蝌孱沐镦翳鲠蜷徕戾轭痱镳泔钿换鏖翳轸狍箝珙邃鲠祯蝈珏蝈痨徙瀛犰ㄣ镱汜翦钺翦篝蜷铉ㄛ捃荀蔹圮荏蔹蕞ㄣ镯磲钿鲠泔眄犷洎ㄛ捃荀蔹圮荏蔹ぉ痱镳泔钿ㄣ镱汜翦钺翦篝蜷铉④茺饼ㄣ镯磲钿屮痱弩箝镱泔眄犷洎④茺昌┅ㄤ彐躅怩忖戾殒ㄣ镯磲钿痱镳泔钿镳糸镱犰麸翎飙泔蝌邈纛弩铋飑⒘痧扉弩翳殒篝狒屙孱蝓戾躞轭泔眄犷麸翳玳鲥痱镳矬轸轱钺祜玳骘蝽蹯岈痱镳泔钿涉珏铄蜥糸铉骘蝽蹯狍骘麸翎泔蝌邈纛弩蟋翳孱麸翎飙泔蝌邈纛弩眭篝忮箦麸寓换碰犰踽翦翳Ⅳ桢睥犷㈠祗澧泔眄犷扉篝篚汨翳狒遽汨箦泗轱换箬秕熹弼孱趱犰禊筢糸箧痱镳泔钿戾è岜ㄢ踱忪瀛泔眄犷潴ㄩ姝泔眄犷洵翳孱泔眄犷洵扉篝泔眄犷洎痱镳泔钿麸翎飙泔蝌邈纛弩螬ㄡㄢ踱忪瀛泔眄犷潴ㄩ姝泔眄犷洵屐箦泔眄犷洵扉篝泔眄犷洎痱镳泔钿麸翎飙泔蝌邈纛弩螬ㄢ镲ㄩ姝泔眄犷洵怙镬遽泔眄犷洎┅ㄦ矧磲铋岍狺岍怙镬岜＼＼怙镬岵┅ㄤ彐躅怩忖戾疳螋獒飙麒殪ㄣ镯磲钿痱镳泔钿⒘痧扉弩翳麒殪瀛篝狒屙孱蝓戾躞轭泔眄犷麸翳玳鲥痱镳矬轸轱钺祜玳骘蝽蹯痱镳泔钿轭翳泔铘屮镦疳螋獒泔蝌邈纛弩螽戾è轭鲠蜷犷痫轭鲠蜷犷趔┅ㄩ铛祆轭鲠蜷犷舂箦翩轭鲠蜷犷㈤铞┅涉铒轭鲠蜷犷骘躅洮溴驷蹯麸㈤铞戾è轫痨殄ㄢ踱忪瀛泔眄犷潴麒殪瀛泔眄犷洵泔眄犷洵扉篝泔眄犷洎轭鲠蜷犷舂ㄢ镲麒殪瀛泔眄犷洵怙镬遽泔眄犷洎┅换馏箦孱轭翳泔躜箦铒翦蠛换嗅螋獒飙麒殪换璃身痨殄换澡弪彐矧换漏璃瘐箬ㄦ矧磲铋狺岍岍轭鲠蜷犷＼＼怙镬痱镳泔钿疳螋獒飙痱镲骟换馏箦孱轭翳泔躜箦铒翦蠛换深鲠蜷犷棂痫翳弩轶犷珲狎换帘身痨殄换漏帘瘐箬ㄦ矧磲铋岍岍轭鲠蜷犷＼怙镬轫痨殄洎疳螋獒飙痱镲骟轭鲠蜷犷舂┅ㄤ彐躅怩忖戾麸翎飙麒殪ㄣ镯磲钿痱镳泔钿⒘痧扉弩翳麒殪瀛篝狒屙孱蝓戾躞轭泔眄犷麸翳玳鲥痱镳矬轸轱钺祜玳骘蝽蹯痱镳泔钿轭翳泔铘屮镦麸翎泔蝌邈纛弩螽戾è轭鲠蜷犷痫轭鲠蜷犷趔┅鲠蜷犷痫鲠蜷犷趔┅ㄩ铛祆轭鲠蜷犷舂箦翩轭鲠蜷犷㈤铞┅涉铒轭鲠蜷犷骘躅洮溴驷蹯麸㈤铞ㄩ铛祆鲠蜷犷舂箦翩鲠蜷犷Ⅵ狎┅涉铒鲠蜷犷骘躅洮溴驷蹯麸Ⅵ狎戾è轫痨殄ㄢ踱忪瀛泔眄犷潴麒殪瀛泔眄犷洵泔眄犷洵扉篝泔眄犷洎ㄦ矧磲铋冀轭轸轭鲠蜷犷＼鲠蜷犷舂舂ㄢ镲麒殪瀛泔眄犷洵怙镬遽泔眄犷洎┅换馏箦孱轭翳泔躜箦铒翦蠛换嗅螋獒飙麒殪换璃身痨殄换澡弪彐矧换漏璃瘐箬ㄦ矧磲铋狺岍岍轭鲠蜷犷＼＼怙镬痱镳泔钿麸翎飙痱镲骟换馏箦孱轭翳泔躜箦铒翦蠛换冀虐深鲠蜷犷棂痫翳弩轶犷珲狎换帘冀疟虐身痨殄换冀虐帘冀疟虐瘐箬ㄦ矧磲铋冀轭轸岍轭鲠蜷犷＼怙镬＼鲠蜷犷轫痨殄洎麸翎飙痱镲骟ㄦ矧磲铋冀幄轭鲠蜷犷＼鲠蜷犷舂┅ㄤ彐躅怩忖戾泔眄犷潴ㄣ镯磲钿痫篝泔钿轸轱镳糸镱犰麸翎飙泔蝌邈纛弩铋飑⒙踱忪弩躔翳蝻蹒扉篝镦泔眄犷骝镯灬篝麸骈蝮犷狃痨殄翳狃痱镳蜷狒蝓戾骘痱秭轭彘翳弪疳螋獒矧麸翎泔蝌邈纛弩骘翳痱镧蜥蝈痱弩孱翦怡翳扉篝泔眄犷潴阵痫篝泔钿轸轱麸箦翳痫篝泔钿轸轱镦翳痱镧蜥懋渝麸翎飙泔蝌邈纛弩麸殒麸翎泔蝌邈纛弩骘蝽蹯狍狎溴箝蝈洧ㄩ铛祆泔眄犷潴铋换迈忖戾翳蝻蹒遽汨泔眄犷洮躔溽糸铉翳沲蝌孱痱镳矬轸轱钺换祜玳泔钿轸轱瞵轭轸獒糸铉轸麸翳痫篝泔钿轸轱戾è蝈鲥蝮瀛泔眄犷潴蝈鲥蝮泔眄犷潴┅ㄣ躜蝈铘痱镳泔钿轸轱痫篝泔钿轸轱瞟祜镳骘泔眄犷轭蝈鲥蝮瀛泔眄犷潴殒ㄣ镯磲钿泔眄犷洎滹箦翩沲蝌孱舡痱镳泔钿轸轱ㄢ踱忪瀛狍箝珙礤铘泔眄犷沲蝌孱舡痱镳泔钿轸轱瞟殒ㄩ姝泔眄犷洵泔眄犷洎滹箦翩沲蝌孱舡痱镳泔钿轸轱ㄢ踱忪瀛殒泔眄犷沲蝌孱舡痱镳泔钿轸轱麸翎飙泔蝌邈纛弩螬殒麒殪瀛泔眄犷洵泔眄犷洎滹痱镧箦翩沲蝌孱舡痱镳泔钿轸轱ㄩ铛祆麸翎飙泔蝌邈纛弩螬ㄢ踱忪瀛疳螋獒飙麒殪泔眄犷沲蝌孱舡痱镳泔钿轸轱瞟ㄢ踱忪瀛麸翎飙麒殪泔眄犷沲蝌孱舡痱镳泔钿轸轱瞟┅┅沲蝌孱舡痱镳泔钿轸轱瞟┅换换歪轭骢钽糸镱换ㄤ彐躅怩殪洵疳蝮瀛趄邋ㄩ铕豸⒁遽潴翳蝻蹒轭瘐犷疳蝮弩轸骘祆秣轭翳挛铒翎糸镱箬秣轭翳泔躜箦怙镫硝麴豸扉篝镦溽翎篝蝓泗躜弩蝈痱弩孱糸铉翳疳蝮邃泔溴涉轸驷殪蟋蝈趱蝾铋膦戾舄è篝蝈犴磲脲篝蜷铉轭瘐舡篝蝈犴轭瘐舂┅蝈徜麒殪篝蝈犴④荏戾è泔眄扉篝疳蝮瀛泔眄犷洵扉篝篝蝈犴┅ㄩㄡ钿铒铛祆泔眄扉篝┅铛祆蝈徜汨狎篝蝈犴铋飑┅泔眄扉篝┅┅ㄤ彐躅汨邈氕痱镧蜥痱邈镱溟糸镱痱镧蜥痫篝泔钿轸轱轭鲠蜷犷趔鲠蜷犷趔⑶孱弪狒弩痱镳矬轸轱钺祜玳骘蝽蹯狍铄邃邃麸痱秭翳鲠扉溟豉镦痱镧蜥玳鲥痱邈镱溟糸镱犷痫篝泔钿轸轱町硝麴豸赭箦趔镦骘蝽蹯狍翳矬铄邃邃骘痱秭轭疳螋獒泔蝌邈纛弩犷翳矬铄邃邃骘痱秭轭麸翎泔蝌邈纛弩螽涉翳弪轶簌铘狲弪蝻轭翳痱镧蜥憩蝈趱蝾铋飚涉翳痱镧蜥泔铘衢铙麒殪祜镳蟋轭鲠蜷犷趔犷鲠蜷犷趔狎遽汨屮疱泗邃麸忮扉篝镦轭鲠蜷犷趔犷鲠蜷犷趔骘祆秣轭翳筢礤矧溴狍翳翦趱犰矧溴镦翳麒殪祜镳螽萧桢蝼轶瀣翳妁汜忮屙痿扉篝螽澡骘蝽蹯狍狎蝈趱蝾邃狍篝蜷铉箦翩疳螋獒飙痱镲骟Ж┅箦翩麸翎飙痱镲骟Ж┅箦翩轭鲠蜷犷趔蝈鲥蝮轭鲠蜷犷趔┅箦翩鲠蜷犷趔蝈鲥蝮鲠蜷犷趔┅换嗅蝮翳痱镧蜥骈蝮戾舄è疳蝮瀛趄邋ㄢ蹰熹疳蝮瀛趄邋痱镧蜥愆ㄩ铞狎獒铘蟓泔瘗ㄣ镳扉篝轭鲠蜷犷趔┅换清铄蜥翦翳疳螋獒泔蝌邈纛弩骘蝽蹯狍疳螋獒飙泔蝌邈纛弩ㄢ踱忪瀛泔眄犷潴疳蝮瀛趄邋ㄦ矧磲铋岍痫篝泔钿轸轱瞟┅箦翩轭鲠蜷犷趔ㄣ镳扉篝轭鲠蜷犷趔泔瘗┅义箦翳轭鲠蜷犷趔换清铄蜥翦翳麸翎泔蝌邈纛弩骘蝽蹯狍戾è麸翎飙泔蝌邈纛弩ㄢ踱忪瀛泔眄犷潴疳蝮瀛趄邋ㄦ矧磲铋岍痫篝泔钿轸轱瞟舂┅ㄩ矧铛祆疳螋獒飙泔蝌邈纛弩螬铛祆麸翎飙泔蝌邈纛弩螬铋ㄦ矧磲铋⑿狎糸犰泔蝌邈纛弩痱镲骟湖エ岍狺狺ピ雉犰泔蝌邈纛弩痱镲骟湖エ岍狺狺痱邈镱溟糸镱疳螋獒飙泔蝌邈纛弩疳螋獒飙痱镲骟痱邈镱溟糸镱麸翎飙泔蝌邈纛弩麸翎飙痱镲骟┅┅ㄤ彐躅磲轭ㄡ蜱雯⒃桢磲轭孱趄痫轭镦翳痱镧蜥懋嗅蝮弩翳玳鲥狎珲礤铘轭麸痱邈镱溟糸镱痱镧蜥憩痫篝泔钿轸轱瞵扉篝镦轭鲠蜷犷趔犷扉篝镦鲠蜷犷趔婶翳孱狒翦眇趔麸珏铄蜥翦骘蝽蹯狍骘痱秭轭翳鲠扉溟豉镦翳痱镧蜥懋涉轸驷殪蟋轸秕麴豸犷弪蝻礤篌徵屮痨衢铋铉麒狒麇铘黩镱纰ㄩ铒窘戾铉翳狎琏穿痱镧黩轸瀛扉铄躞徵瀛礤篌徵濠筲屮艉屮轸┅戾è痱邈镱溟糸镱铘狎琏┅痱镧蜥铘狎琏┅痫篝泔钿轸轱铘狎琏┅ㄩ铞鲠颦戾铉翳ō戾铉翳狎琏穿┅换歪脲篚蝈翳狒翳麸翎铛礅弪镦轭鲠蜷犷趔犷鲠蜷犷趔换轶弼孱ㄩ铒盹轭霏鲠颦戾铉翳博癌黩轸瀛扉铄轭霏鲠颦弪蝻颟戾舄è轭霏戾铉翳ǒ轭霏鲠颦戾铉翳博ㄩ铞狎獒铘篚怏羼狎琏ǐ轭霏戾铉翳穿┅鲠蜷犷趔篚怏羼狎琏ǐ轭霏戾铉翳穿戾铉翳狎琏┅换阴翳痱镧蜥蝈篚祠ㄣ桢汶痱镧蜥痱邈镱溟糸镱痱镧蜥痫篝泔钿轸轱轭鲠蜷犷趔鲠蜷犷趔┅ㄩ铛祆蝈篚祠黩轸瀛扉铄弪蝻颦礤篌徵濠黩轸瀛扉铄蝈篚祠┅筲屮艉屮轸┅┅┅